import mutable/array
import src/effekt/benchmark
import immutable/list
import src/effekt/cliRunner

//each list represents one smallerDisk.
type Disk = List[Int]

def Towers() = {

  var piles: Array[Disk] = emptyArray();
  var movesDone: Int = 0;

  def pushDisk(piles: Array[Disk], smallerDisk: Disk, pileIdx: Int): Disk = {
    val currentTopDisk: Disk = piles.unsafeGet(pileIdx);
    (smallerDisk, currentTopDisk) match {
      case (Cons(smallerSize,_), Cons(largerSize,_)) => 
        if (smallerSize >= largerSize) {
          panic("Cannot put a big smallerDisk on a smaller one:"++show(piles));
        }
    }

    //push current top disk one down, put smaller on top
    val replacer = smallerDisk match { //smallerDisk.next = currentTopDisk
      case Cons(smallerSize, next) => 
        Cons(smallerSize, currentTopDisk)
      case Nil() => panic("tried putting nothing on top of stack")
    }
    put(piles,pileIdx, replacer);
    return currentTopDisk 
  }

  def popDiskFrom(pileIdx: Int): Disk = {
    val currentTopDisk = piles.unsafeGet(pileIdx);
    
    currentTopDisk match {
      case Nil() => panic("Attempting to remove a smallerDisk from an empty pileIdx");
      case Cons(size, next) => 
      put(piles, pileIdx, next)
      return Cons(size, Nil())
    }
  }

  def moveTopDisk(fromPile: Int, toPile: Int) = {
    pushDisk(piles, popDiskFrom(fromPile), toPile);
    movesDone = movesDone + 1;
  }

  def buildTowerAt(pileIdx: Int, disks: Int) = {
    var i = disks;
    while (i >= 0) {
      pushDisk(piles, Cons(i,Nil()), pileIdx);
      i = i -1;
    }
  }

  def moveDisks(disks: Int, fromPile: Int, toPile: Int): Unit = {
    if (disks == 1) {
      moveTopDisk(fromPile, toPile);
    } else {
      val otherPile = (3 - fromPile) - toPile;
      moveDisks(disks - 1, fromPile, otherPile);
      moveTopDisk(fromPile, toPile);
      moveDisks(disks - 1, otherPile, toPile);
    }
  }

  def benchmark(): Int = {
    piles = emptyArray(3)
    put[Disk](piles,0,Nil())
    put[Disk](piles,1,Nil())
    put[Disk](piles,2,Nil())

    buildTowerAt(0, 13);

    movesDone = 0;
    moveDisks(13, 0, 1);
    return movesDone;
  }

  def verifyResult(result: Int): Boolean = {
    return result == 8191;
  }

  innerBenchmarkLoop(1){benchmark}{verifyResult}
}

def diskSize(d: Disk): Int = {
  d match {
    case Cons(s,_) => s
    case Nil() => -1
  }
}

def miniRun() = {
  Towers()
}

def normalRun() = {
  each(0,100){ i => 
    Towers()
  }
}

def main() = {
  println(runFromCli{ miniRun }{ normalRun })
}